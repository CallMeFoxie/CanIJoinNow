buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'

sourceCompatibility = targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}


ext.configFile = file "build.properties"

configFile.withReader {
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}

def VersionBuild = 0
if (System.getenv("BUILD_NUMBER") != null) {
    def buildver = System.getenv("BUILD_NUMBER")
    VersionBuild = "-${buildver}"
}

version = "${config.minecraft_version}-${config.mod_version}${VersionBuild}"
group = "foxie"
archivesBaseName = "CanIJoinNow"

minecraft {
    version = config.minecraft_version + "-" + config.forge_version
    runDir = "run"
    mappings = config.mappings
    replaceIn "foxie/CanIJoinNow/CanIJoinNow.java"
    replace "@VERSION@", "${config.mod_version}"
}

repositories {
    maven {
        name "Foxiehost"
        url "http://maven.foxiehost.eu"
    }
}

dependencies {
    testCompile 'junit:junit:4.12'
}

processResources
        {
            inputs.property "version", project.version
            inputs.property "mcversion", project.minecraft.version

            from(sourceSets.main.resources.srcDirs) {
                include 'mcmod.info'

                expand 'version': project.version, 'mcversion': project.minecraft.version
            }
            from(sourceSets.main.resources.srcDirs) {
                exclude 'mcmod.info'
            }
        }
/*jar {
    manifest {
        attributes 'FMLCorePlugin': 'foxie.CanIJoinNow.asm.CanIJoinNowCoreLoader'
        attributes 'FMLCorePluginContainsFMLMod': 'true'
    }
}*/

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
}

task apiJar(type: Jar) {
    from(sourceSets.main.java) {
        include "foxie/CanIJoinNow/api/"
    }
    classifier = 'api'
}

artifacts {
    archives deobfJar
    archives apiJar
}

build.dependsOn deobfJar, apiJar

// deployment
configurations { deployerJars }
dependencies { deployerJars "org.apache.maven.wagon:wagon-ssh:2.2" }

apply plugin: 'maven'
uploadArchives {
    repositories {
        mavenDeployer {
            configuration = configurations.deployerJars

            repository(url: "scp://maven.foxiehost.lan/home/maven/public/") {
                authentication(userName: "maven", password: "maven")
            }

            pom {
                groupId = project.group
                version = project.version
                artifactId = project.archivesBaseName
                project {
                    name project.archivesBaseName
                    packaging 'jar'
                    description 'CanIJoinNow'
                    url 'http://foxiemods.com/CanIJoinNow'
                    scm {
                        url 'https://github.com/CallMeFoxie/CanIJoinNow'
                        connection 'scm:git:git@github.com:CallMeFoxie/CanIJoinNow.git'
                        developerConnection 'scm:git:git@github.com:CallMeFoxie/CanIJoinNow.git'
                    }
                    issueManagement {
                        system 'github'
                        url 'https://github.com/CallMeFoxie/CanIJoinNow/issues'
                    }
                    licenses {
                        license {
                            name 'TBD'
                            url 'https://does-not-exist'
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id 'CallMeFoxie'
                            name 'CallMeFoxie'
                            roles { role 'developer' }
                        }
                    }
                }
            }
        }
    }
}
